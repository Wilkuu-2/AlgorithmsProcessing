/*
  3D Perlin Noise
    The 3D Perlin noise example with added time dimension
    
    ORIGINAL:
      // Daniel Shiffman
      // The Nature of Code
      // http://natureofcode.com
    
    Copyright 2022 Jakub Stachurski and Jules Verhagen 
*/

// -- GLOBALS 



float increment = 0.02;
float timeIncrement = 0.02;
long time = 0; 

void setup() {
  size(640,360);
}

void draw() {
  background(0);
  
  // Optional: adjust noise detail here
  noiseDetail(8,0.65f);
  
  loadPixels();
  float timeOffset = timeIncrement * time;
  
  float xoff = 0.0; // Start xoff at 0
  // For every x,y coordinate in a 2D space, calculate a noise value and produce a brightness value
  for (int x = 0; x < width; x++) {
    xoff += increment;   // Increment xoff 
    float yoff = 0.0;   // For every xoff, start yoff at 0
    for (int y = 0; y < height; y++) {
      yoff += increment; // Increment yoff
      
      // Calculate noise and scale by 255
      float bright = noise(xoff,yoff,timeOffset)*255;

      // Try using this line instead
      //float bright = random(0,255);
      
      // Set each pixel onscreen to a grayscale value
      pixels[x+y*width] = color(bright);
    }
  }
  time++;
  updatePixels();
}
